plugins {
    id 'com.gradle.plugin-publish' version '0.9.1'
}

apply from: "${rootDir}/gradle/compileGroovy.gradle"
apply from: "${rootDir}/gradle/integTest.gradle"
apply from: "${rootDir}/gradle/codenarc.gradle"
apply from: "${rootDir}/gradle/jacoco.gradle"

description 'Gradle plugin for running SoapUI projects'
group 'org.urbanbyte'
version = '0.4.1'
targetCompatibility = 1.6
sourceCompatibility = 1.6

repositories {
    if( project.hasProperty('internalArtifactRepository') ){
        maven { url project.internalArtifactRepository }
    } else {
        maven { url 'http://smartbearsoftware.com/repository/maven2/' }
        maven { url 'http://smartbearsoftware.com/repository/maven2/gnu' }
        mavenCentral()
        jcenter()
    }
}

dependencies {
    compile localGroovy()
    compile gradleApi()
    
    /**
     * SoapUI dependencies
     */
    compile ('com.smartbear.soapui:soapui:5.1.0') {
        exclude module: 'commons-logging'
        exclude module: 'log4j'
        exclude module: 'jtidy'
        exclude module: 'cajo'
        exclude group: 'org.codehaus.groovy'
    }
    
    testCompile ('com.netflix.nebula:nebula-test:2.2.+'){
        exclude group: 'org.codehaus.groovy'
    }
    
    configurations.all {
    resolutionStrategy {
        //failOnVersionConflict()
        force 'log4j:log4j:1.2.10',
                'commons-codec:commons-codec:1.4',
                'commons-io:commons-io:1.4',
                'org.apache.httpcomponents:httpcore:4.2.5',
                'org.apache.httpcomponents:httpclient:4.2.5',
                'commons-logging:commons-logging:1.1.1',
                'commons-lang:commons-lang:2.4',
                'xalan:xalan:2.7.1',
                'xml-apis:xml-apis:1.3.04',
                'junit:junit:4.10',
                'xom:xom:1.1',
                'org.apache.santuario:xmlsec:1.4.5'
       }
    }
}

pluginBundle {
    website = 'https://github.com/creising/soapui-gradle-plugin'
    vcsUrl = 'https://github.com/creising/soapui-gradle-plugin'
    description = 'Gradle plugin for running SoapUI projects'
    tags = ['soapui', 'testing', 'soap', 'rest']

    plugins {
        soapuiPlugin {
            id = 'org.urbanbyte.soapui'
            displayName = 'Gradle plugin for running SoapUI projects'
        }
    }
}

wrapper {
    gradleVersion = 2.3
}
